import org.gradle.internal.os.OperatingSystem

group 'trift.vs.rest'
version '1.0-SNAPSHOT'

apply plugin: 'java'

buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
        dockerPluginVersion = '0.8.2'
    }
    repositories {
        jcenter()
        mavenCentral()
        ivy {
            artifactPattern "http://dl.bintray.com/bsideup/thirdparty/[artifact]-[revision](-[classifier]).[ext]"
            artifactPattern "http://dl.bintray.com/aatarasoff/maven/[artifact]-[revision](-[classifier]).[ext]"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
        classpath "com.bmuschko:gradle-docker-plugin:${dockerPluginVersion}"
        classpath 'ru.trylogic.gradle.plugins:gradle-thrift-plugin:0.1.1'
    }
}

ext {
    thriftVersion = '0.9.2'
    dropwizardMetricsVersion = '3.1.2'
    //springCloudStarterConsulAllVersion = "1.0.0.M3"
    feignAndRibboonVersion = "1.0.6.RELEASE"
}

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'provided-base'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.M3"
        }

        dependencies {
            dependency "org.apache.thrift:libthrift:${thriftVersion}"
            dependency 'info.developerblog.spring.thrift:spring-thrift-starter:0.1.4'
            dependency "org.springframework.cloud:spring-cloud-starter-consul-all:1.0.0.M3"
            /*dependency "org.springframework.cloud:spring-cloud-consul-core:1.0.0.M3"
            dependency "org.springframework.cloud:spring-cloud-consul-discovery:1.0.0.M3"
            dependency "org.springframework.cloud:spring-cloud-consul-config:1.0.0.M3"*/
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'spring-boot'
    apply plugin: 'thrift'

    task generateThriftJava(type: ru.trylogic.gradle.thrift.tasks.ThriftCompileTask) {
        generator = 'java:private-members'
        destinationDir = file("generated-src/generated/java")
    }


    sourceSets {
        main {
            java {
                srcDir generateThriftJava.destinationDir
            }
        }
    }

    clean {
        delete generateThriftJava.destinationDir
    }

    idea {
        module {
            sourceDirs += file('src/main/thrift')
            sourceDirs += generateThriftJava.destinationDir
        }
    }

    compileJava.dependsOn generateThriftJava


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        archiveName = project.name + ".jar"
    }

    repositories {
        ivy {
            artifactPattern "http://dl.bintray.com/bsideup/thirdparty/[artifact]-[revision](-[classifier]).[ext]"
            artifactPattern "http://dl.bintray.com/aatarasoff/maven/[artifact]-[revision](-[classifier]).[ext]"
        }
    }

    dependencies {
        Map platformMapping = [
                (OperatingSystem.WINDOWS): 'win',
                (OperatingSystem.MAC_OS) : 'osx'
        ].withDefault { 'nix' }

        thrift "org.apache.thrift:thrift:$thriftVersion:${platformMapping.get(OperatingSystem.current())}@bin"

        compile "org.apache.thrift:libthrift"
        compile 'org.codehaus.groovy:groovy-all'

        compile "org.projectlombok:lombok:1.16.6"

        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    task dockerBuild << {
        exec {
            commandLine 'docker', 'build', '--force-rm=true', "--tag='${project.name}'", '.'
        }
    }

    dockerBuild.dependsOn build

    springBoot {
        executable = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}