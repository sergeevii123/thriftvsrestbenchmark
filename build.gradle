import org.gradle.internal.os.OperatingSystem
def projects = { list, config -> list.each { project(it, config) } }

buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
        dockerPluginVersion = '0.8.2'
    }
    repositories {
        jcenter()
        mavenCentral()
        ivy {
            artifactPattern "http://dl.bintray.com/bsideup/thirdparty/[artifact]-[revision](-[classifier]).[ext]"
            artifactPattern "http://dl.bintray.com/aatarasoff/maven/[artifact]-[revision](-[classifier]).[ext]"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
        classpath "com.bmuschko:gradle-docker-plugin:${dockerPluginVersion}"
        classpath 'ru.trylogic.gradle.plugins:gradle-thrift-plugin:0.1.1'
    }
}

ext {
    thriftVersion = '0.9.2'
    dropwizardMetricsVersion = '3.1.2'
    feignAndRibboonVersion = "1.0.6.RELEASE"

    executeCommand = { def command, long delay = 0, throwOnException = false ->
        logger.info("::Execute [$command]")
        Process process = command.execute()
        def output = new StringBuffer()
        process.consumeProcessOutput(output, output)
        process.waitFor()
        if (throwOnException && process.exitValue() != 0){
            logger.error(output.toString())
            throw new GradleException("Execution failed for command $command")
        }
        logger.debug(output.toString())
        sleep delay
        return output
    }

    getContainerIP = { def containerName ->
        executeCommand("docker inspect --format {{.NetworkSettings.IPAddress}} $containerName").toString().trim()
    }

    hostForTestEnv = System.getProperty("host.for.test", "172.17.0.1")
    mode = System.getProperty("test.mode", "mode1")
    threadCount = System.getProperty("thread.count", Integer.toString(2))
    fileLength = System.getProperty("file.length", Integer.toString(2*1024*1024))
    duration = System.getProperty("duration", Integer.toString(30))
    consulIP = System.getProperty("consul.for.test", getContainerIP("consul"))
}


subprojects {
    apply plugin: 'base'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'spring-boot'
    apply plugin: 'thrift'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'provided-base'

    repositories {
        maven {url "http://repo.springsource.org/milestone/"}
        mavenCentral()
        jcenter()
        ivy {
            artifactPattern "http://dl.bintray.com/bsideup/thirdparty/[artifact]-[revision](-[classifier]).[ext]"
            artifactPattern "http://dl.bintray.com/aatarasoff/maven/[artifact]-[revision](-[classifier]).[ext]"
        }
    }

    dependencyManagement {

        dependencies {
            dependency "com.fasterxml.jackson.core:jackson-databind:2.6.4"
            dependency "com.fasterxml.jackson.core:jackson-annotations:2.6.4"
            dependency "info.developerblog.spring.thrift:spring-thrift-starter:0.9.2"
            dependency "org.springframework.boot:spring-boot:1.3.0.RELEASE"
            dependency "org.springframework.cloud:spring-cloud-starter-ribbon:1.0.6.RELEASE"
            dependency "org.springframework.boot:spring-boot-starter-actuator:1.3.0.RELEASE"
            dependency "org.springframework.boot:spring-boot-starter-web:1.3.0.RELEASE"
            dependency "org.springframework.cloud:spring-cloud-starter-feign:1.0.6.RELEASE"
            dependency "org.springframework.boot:spring-boot-starter-actuator:1.3.0.RELEASE"
            dependency "org.springframework.cloud:spring-cloud-starter-hystrix:1.0.6.RELEASE"
            dependency "org.projectlombok:lombok:1.16.4"
            dependency "ch.qos.logback:logback-classic:1.1.3"
            dependency 'com.netflix.feign:feign-core:8.16.0'
            dependency "org.codehaus.groovy:groovy:2.4.4"
            dependency "org.slf4j:jcl-over-slf4j:1.7.12"
            dependency "io.dropwizard.metrics:metrics-core:3.1.2"
            dependency "org.codehaus.groovy:groovy-all:2.4.4"
            dependency "ch.qos.logback:logback-classic:1.1.3"
            dependency "org.apache.thrift:libthrift:${thriftVersion}"
            dependency("org.springframework.cloud:spring-cloud-starter-consul-all:1.0.0.RC1") {
                exclude group: "io.reactivex", module: "rxjava"
            }
            dependency "io.reactivex:rxjava:1.1.2"
        }
    }

    task generateThriftJava(type: ru.trylogic.gradle.thrift.tasks.ThriftCompileTask) {
        generator = 'java:private-members'
        destinationDir = file("generated-src/generated/java")
    }

    sourceSets {
        main {
            java {
                srcDir generateThriftJava.destinationDir
            }
        }
    }

    clean {
        delete generateThriftJava.destinationDir
    }

    idea {
        module {
            sourceDirs += file('src/main/thrift')
            sourceDirs += generateThriftJava.destinationDir
        }
    }

    compileJava.dependsOn generateThriftJava


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        archiveName = project.name + ".jar"
    }

    dependencies {
        Map platformMapping = [
                (OperatingSystem.WINDOWS): 'win',
                (OperatingSystem.MAC_OS) : 'osx'
        ].withDefault { 'nix' }

        thrift "org.apache.thrift:thrift:$thriftVersion:${platformMapping.get(OperatingSystem.current())}@bin"

        compile "org.apache.thrift:libthrift"
        compile 'org.codehaus.groovy:groovy-all'

        compile "org.projectlombok:lombok:1.16.6"

        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    task dockerBuild << {
        exec {
            commandLine 'docker', 'build', '--force-rm=true', "--tag=${project.name}", '.'
        }
    }

    dockerBuild.dependsOn build

    springBoot {
        executable = true
    }
}

projects(['test']) {
    //def consulIP = getContainerIP("consul")

    bootRun {
        doFirst {
            systemProperty "spring.cloud.consul.host", consulIP
            systemProperty "server.port", "1234"
            systemProperty "spring.profiles.active", mode
            systemProperty "thread.count", threadCount
            systemProperty "file.length", fileLength
            systemProperty "duration", duration
        }
    }

}

task startDockers(type: Exec) {
    environment "HOST_FOR_TEST": hostForTestEnv, "TEST_MODE" : mode
    commandLine "docker-compose", "up", "-d"
}
